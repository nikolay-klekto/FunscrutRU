apply plugin: 'idea'
apply plugin: 'groovy'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/itest/java')
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/itest/groovy')
        }
        resources.srcDir file('src/itest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

idea {
    module {
        //and some extra test source dirs
        ['java', 'groovy', 'kotlin'].each {
            testSourceDirs += file("src/itest/$it")
        }
        scopes.TEST.plus += [configurations.integrationTestCompile, configurations.integrationTestRuntime]
    }
}

// Define integration test task
task integrationTest(type: Test) {
    def v = project.getGradle().getGradleVersion().trim()
    if (v.startsWith('2') || v.startsWith('3')) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
    } else {
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
    }

    classpath = sourceSets.integrationTest.runtimeClasspath
}

